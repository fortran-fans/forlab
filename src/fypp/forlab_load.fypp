#:include 'common.fypp'
submodule(forlab) forlab_load
    ! loadbin
    !-----------------------------------------------------------------------
    ! loadbin loads binary files.
    !
    ! Syntax
    !-----------------------------------------------------------------------
    ! x = loadbin(filename)
    ! x = loadbin(filename, kind)
    ! x = loadbin(filename, kind, dim1)
    ! A = loadbin(filename, kind, dim1, dim2)
    ! X = loadbin(filename, kind, dim1, dim2, dim3)
    !
    ! Description
    !-----------------------------------------------------------------------
    ! x = loadbin(filename) loads a 1-dimensional array into x from the
    ! binary file filename treated as 32 bytes floating points.
    !
    ! x = loadbin(filename, kind) loads a 1-dimensional array into x from
    ! the binary file filename.
    !
    ! x = loadbin(filename, kind, dim1) loads a 1-dimensional array into x
    ! from the binary file filename.
    !
    ! A = loadbin(filename, kind, dim1, dim2) loads a 2-dimensional array
    ! into A from the binary file filename.
    !
    ! X = loadbin(filename, kind, dim1, dim2, dim3) loads a 3-dimensional
    ! array into X from the binary file filename.
    !
    ! Notes
    !-----------------------------------------------------------------------
    ! Make sure to use the exact kind:
    !   -   4 for 32 bytes floating points,
    !   -   8 for 64 bytes floating points.


    ! loadtxt
    !-----------------------------------------------------------------------
    ! loadtxt loads txt files.
    !
    ! Syntax
    !-----------------------------------------------------------------------
    ! x = loadtxt(filename)
    ! A = loadtxt(filename, dim2)
    !
    ! Description
    !-----------------------------------------------------------------------
    ! x = loadtxt(filename) loads a 1-dimensional array into x from a txt
    ! file filename.
    !
    ! A = loadtxt(filename, dim2) loads a 2-dimensional array into A from a
    ! txt file filename. dim2 indicates the number of columns of the array.

    use forlab_kinds
    implicit none

contains
    #:for k1, t1 in REAL_KINDS_TYPES
    module procedure loadbin_0_${k1}$
        integer :: opt_kind, dim1, fs
        type(File) :: infile

        opt_kind = ${k1}$

        infile = File(trim(filename))
        if (infile%exist()) then
            inquire (file=filename, size=fs)
            if (mod(fs, opt_kind) .eq. 0) then
                dim1 = fs/opt_kind
                allocate (loadbin_0_${k1}$(dim1))
                call infile%open(opt_kind*dim1)
                read (infile%unit, rec=1) loadbin_0_${k1}$
                call infile%close()
            else
                print *, "Error: in loadbin, file size mismatches kind."
                stop
            end if
        else
            print *, "Error: '"//trim(filename)//"' not found"
            stop
        end if
        return
    end procedure

    module procedure loadbin_1_${k1}$
        type(File) :: infile

        infile = File(trim(filename))
        if (infile%exist()) then
            allocate (loadbin_1_${k1}$(dim1))
            call infile%open(${k1}$*dim1)
            read (infile%unit, rec=1) loadbin_1_${k1}$
            call infile%close()
        else
            print *, "Error: '"//trim(filename)//"' not found"
            stop
        end if
        return
    end procedure

    module procedure loadbin_2_${k1}$
        type(File) :: infile

        infile = File(trim(filename))
        if (infile%exist()) then
            allocate (loadbin_2_${k1}$(dim1, dim2))
            call infile%open(${k1}$*dim1*dim2)
            read (infile%unit, rec=1) loadbin_2_${k1}$
            call infile%close()
        else
            print *, "Error: '"//trim(filename)//"' not found"
            stop
        end if
        return
    end procedure

    module procedure loadbin_3_${k1}$
        type(File) :: infile

        infile = File(trim(filename))
        if (infile%exist()) then
            allocate (loadbin_3_${k1}$(dim1, dim2, dim3))
            call infile%open(${k1}$*dim1*dim2*dim3)
            read (infile%unit, rec=1) loadbin_3_${k1}$
            call infile%close()
        else
            print *, "Error: '"//trim(filename)//"' not found"
            stop
        end if
        return
    end procedure

    ! loadtxt
    !-----------------------------------------------------------------------
    ! loadtxt loads txt files.
    !
    ! Syntax
    !-----------------------------------------------------------------------
    ! x = loadtxt(filename)
    ! A = loadtxt(filename, dim2)
    !
    ! Description
    !-----------------------------------------------------------------------
    ! x = loadtxt(filename) loads a 1-dimensional array into x from a txt
    ! file filename.
    !
    ! A = loadtxt(filename, dim2) loads a 2-dimensional array into A from a
    ! txt file filename. dim2 indicates the number of columns of the array.

    module procedure loadtxt_1_${k1}$
        integer :: i, m
        type(File) :: infile

        infile = File(trim(filename))
        if (infile%exist()) then
            m = infile%countlines()
            allocate (loadtxt_1_${k1}$(m))
            call infile%open()
            do i = 1, m
                read (infile%unit, *) loadtxt_1_${k1}$(i)
            end do
            call infile%close()
        else
            print *, "Error: '"//trim(filename)//"' not found"
            stop
        end if
        return
    end procedure

    module procedure loadtxt_2_${k1}$
        integer :: i, j, m
        type(File) :: infile

        infile = File(trim(filename))
        if (infile%exist()) then
            m = infile%countlines()
            allocate (loadtxt_2_${k1}$(m, dim2))
            call infile%open()
            do i = 1, m
                read (infile%unit, *) (loadtxt_2_${k1}$(i, j), j=1, dim2)
            end do
            call infile%close()
        else
            print *, "Error: '"//trim(filename)//"' not found"
            stop
        end if
        return
    end procedure

    #:endfor
end submodule
