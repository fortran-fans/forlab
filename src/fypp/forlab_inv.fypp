#:include "common.fypp"

submodule(forlab) forlab_inv
    use forlab_kinds

contains
    #:for k1,t1 in REAL_KINDS_TYPES
        function inv_${t1[0]}$${k1}$ (A)
        !! inv0 computes the real matrix inverse.
            ${t1}$, dimension(:, :), allocatable :: inv_${t1[0]}$${k1}$
            ${t1}$, dimension(:, :), intent(in) :: A
            integer :: i, j, k, m
            real(${k1}$) :: D
            real(${k1}$), dimension(:), allocatable :: x, y, e
            real(${k1}$), dimension(:, :), allocatable :: L, U

            if (issquare(A)) then
                m = size(A, 1)
                if (m .le. 3) then
                    D = det(A)
                else
                    D = det(A, L, U)
                end if
                if (D .ne. 0.) then
                    inv_${t1[0]}$${k1}$ = zeros(m, m)
                    if (m .eq. 2) then
                        inv_${t1[0]}$${k1}$ (1, 1) = A(2, 2)
                        inv_${t1[0]}$${k1}$ (1, 2) = -A(1, 2)
                        inv_${t1[0]}$${k1}$ (2, 1) = -A(2, 1)
                        inv_${t1[0]}$${k1}$ (2, 2) = A(1, 1)
                        inv_${t1[0]}$${k1}$ = inv_${t1[0]}$${k1}$/D
                    elseif (m .eq. 3) then
                        inv_${t1[0]}$${k1}$ (1, 1) = A(2, 2)*A(3, 3) - A(2, 3)*A(3, 2)
                        inv_${t1[0]}$${k1}$ (1, 2) = A(1, 3)*A(3, 2) - A(1, 2)*A(3, 3)
                        inv_${t1[0]}$${k1}$ (1, 3) = A(1, 2)*A(2, 3) - A(1, 3)*A(2, 2)
                        inv_${t1[0]}$${k1}$ (2, 1) = A(2, 3)*A(3, 1) - A(2, 1)*A(3, 3)
                        inv_${t1[0]}$${k1}$ (2, 2) = A(1, 1)*A(3, 3) - A(1, 3)*A(3, 1)
                        inv_${t1[0]}$${k1}$ (2, 3) = A(1, 3)*A(2, 1) - A(1, 1)*A(2, 3)
                        inv_${t1[0]}$${k1}$ (3, 1) = A(2, 1)*A(3, 2) - A(2, 2)*A(3, 1)
                        inv_${t1[0]}$${k1}$ (3, 2) = A(1, 2)*A(3, 1) - A(1, 1)*A(3, 2)
                        inv_${t1[0]}$${k1}$ (3, 3) = A(1, 1)*A(2, 2) - A(1, 2)*A(2, 1)
                        inv_${t1[0]}$${k1}$ = inv_${t1[0]}$${k1}$/D
                    else
                        do k = 1, m
                            x = zeros(m)
                            y = zeros(m)
                            e = zeros(m)
                            e(k) = 1.
                            y(1) = e(1)

                            ! Forward substitution: Ly = e
                            !==============================
                            do i = 2, m
                                y(i) = e(i)
                                do j = 1, i - 1
                                    y(i) = y(i) - y(j)*L(i, j)
                                end do
                            end do

                            ! Back substitution: Ux = y
                            !===========================
                            x(m) = y(m)/U(m, m)
                            do i = m - 1, 1, -1
                                x(i) = y(i)
                                do j = m, i + 1, -1
                                    x(i) = x(i) - x(j)*U(i, j)
                                end do
                                x(i) = x(i)/U(i, i)
                            end do

                            ! The column k of the inverse is x
                            !==================================
                            inv_${t1[0]}$${k1}$ (:, k) = x
                        end do
                    end if
                else
                    stop "Error: in det(A), A is not inversible (= 0)."
                end if
            else
                stop "Error: in inv(A), A should be square."
            end if
            return
        end function

    #:endfor

    #:for k1,t1 in CMPLX_KINDS_TYPES

        function inv_${t1[0]}$${k1}$ (A)
        !! inv computes the complex matrix inverse.
            ${t1}$, dimension(:, :), allocatable :: inv_${t1[0]}$${k1}$
            ${t1}$, dimension(:, :), intent(in) :: A
            real(${k1}$), dimension(:, :), allocatable :: ar, ai
        !! AR stores the real part, AI stores the imaginary part
            integer :: flag, n
            real(${k1}$) :: d, p, t, q, s, b
            integer, dimension(:), allocatable :: is, js
            integer :: i, j, k

            if (issquare(A)) then
                n = size(A, 1)
                inv_${t1[0]}$${k1}$ = zeros(n, n)
                ar = zeros(n, n)
                ai = zeros(n, n)
                allocate(is(n),js(n))
                is = 0
                js = 0
                forall (i=1:n, j=1:n)
                    ar(i, j) = real(A(i, j)); ai(i, j) = imag(A(i, j))
                end forall
                flag = 1
                do k = 1, n
                    d = 0.0
                    do i = k, n
                        do j = k, n
                            p = ar(i, j)*ar(i, j) + ai(i, j)*ai(i, j)
                            if (p .gt. d) then
                                d = p
                                is(k) = i
                                js(k) = j
                            end if
                        end do
                    end do
                    if (d + 1.0 .eq. 1.0) then
                        flag = 0
                        stop 'ERROR: A is not inversible (= 0)'
                    end if
                    do j = 1, n
                        t = ar(k, j)
                        ar(k, j) = ar(is(k), j)
                        ar(is(k), j) = t
                        t = ai(k, j)
                        ai(k, j) = ai(is(k), j)
                        ai(is(k), j) = t
                    end do
                    do i = 1, n
                        t = ar(i, k)
                        ar(i, k) = ar(i, js(k))
                        ar(i, js(k)) = t
                        t = ai(i, k)
                        ai(i, k) = ai(i, js(k))
                        ai(i, js(k)) = t
                    end do
                    ar(k, k) = ar(k, k)/d
                    ai(k, k) = -ai(k, k)/d
                    do j = 1, n
                        if (j .ne. k) then
                            p = ar(k, j)*ar(k, k)
                            q = ai(k, j)*ai(k, k)
                            s = (ar(k, j) + ai(k, j))*(ar(k, k) + ai(k, k))
                            ar(k, j) = p - q
                            ai(k, j) = s - p - q
                        end if
                    end do
                    do i = 1, n
                        if (i .ne. k) then
                            do j = 1, n
                                if (j .ne. k) then
                                    p = ar(k, j)*ar(i, k)
                                    q = ai(k, j)*ai(i, k)
                                    s = (ar(k, j) + ai(k, j))*(ar(i, k) + ai(i, k))
                                    t = p - q
                                    b = s - p - q
                                    ar(i, j) = ar(i, j) - t
                                    ai(i, j) = ai(i, j) - b
                                end if
                            end do
                        end if
                    end do
                    do i = 1, n
                        if (i .ne. k) then
                            p = ar(i, k)*ar(k, k)
                            q = ai(i, k)*ai(k, k)
                            s = (ar(i, k) + ai(i, k))*(ar(k, k) + ai(k, k))
                            ar(i, k) = q - p
                            ai(i, k) = p + q - s
                        end if
                    end do
                end do
                do k = n, 1, -1
                    do j = 1, n
                        t = ar(k, j)
                        ar(k, j) = ar(js(k), j)
                        ar(js(k), j) = t
                        t = ai(k, j)
                        ai(k, j) = ai(js(k), j)
                        ai(js(k), j) = t
                    end do
                    do i = 1, n
                        t = ar(i, k)
                        ar(i, k) = ar(i, is(k))
                        ar(i, is(k)) = t
                        t = ai(i, k)
                        ai(i, k) = ai(i, is(k))
                        ai(i, is(k)) = t
                    end do
                end do
                forall (i=1:n, j=1:n)
                    inv_${t1[0]}$${k1}$ (i, j) = dcmplx(ar(i, j), ai(i, j))
                end forall
            else
                stop 'Error: in inv(A), A should be square.'
            end if
            return
        end function
    #:endfor
end submodule
