#:include 'common.fypp'
!! This file contains the multi-precision `randi` function implementation.

submodule(forlab) forlab_randi
    !! Version: experimental
    !!
    !!## randi
    !! randi generates uniformly distributed random integers.
    !!
    !!### Syntax
    !!     x = randi(imax)
    !!     x = randi([imin, imax])
    !!     x = randi(imax, dim1)
    !!     x = randi([imin, imax], dim1)
    !!     A = randi(imax, dim1, dim2)
    !!     A = randi([imin, imax], dim1, dim2)
    !!     X = randi(imax, dim1, dim2, dim3)
    !!     X = randi([imin, imax], dim1, dim2, dim3)
    !!
    !!### Description
    !! `x = randi(imax)` returns a random scalar integer between `1` and `imax`.
    !!
    !! `x = randi([imin, imax])` returns a random scalar integer between `imin`
    !! and `imax`.
    !!
    !! `x = randi(imax, dim1)` returns a dim1 vector of random scalar integers
    !! between `1` and `imax`.
    !!
    !! `x = randi([imin, imax], dim1)` returns a dim1 vector of random scalar
    !! integers between `imin` and `imax`.
    !!
    !! `A = randi(imax, dim1, dim2)` returns a dim1-by-dim2 matrix of random
    !! scalar integers between `1` and `imax`.
    !!
    !! `A = randi([imin, imax], dim1, dim2)` returns a dim1-by-dim2 matrix of
    !! random scalar integers between `imin` and `imax`.
    !!
    !! `X = randi(imax, dim1, dim2, dim3)` returns a dim1-by-dim2-by-dim3
    !! 3-dimensional matrix of random scalar integers between `1` and `imax`.
    !!
    !! `X = randi([imin, imax], dim1, dim2, dim3)` returns a dim1-by-dim2-by-dim3
    !! 3-dimensional matrix of random scalar integers between `imin` and `imax`.
    use forlab_kinds
    implicit none

contains
    #:set RANKS = range(0, 4)
    #:set RANKS2 = range(0, 2)
    #:for RANK in RANKS
    #:for RANK2 in RANKS2
    module procedure randi_${RANK}$_${RANK2}$
    #:if RANK == 0
        #:if RANK2 == 0
        randi_${RANK}$_${RANK2}$ = floor(randu()*real(imax)) + 1
        #:elif RANK2 == 1
        randi_${RANK}$_${RANK2}$ = minval(imax) + nint(randu()*real(maxval(imax) - minval(imax)))
        #:endif
    #:elif RANK == 1
        #:if RANK2 == 0
        randi_${RANK}$_${RANK2}$ = floor(randu(dim1)*real(imax)) + 1
        #:elif RANK2 == 1
        randi_${RANK}$_${RANK2}$ = minval(imax) + nint(randu(dim1)*real(maxval(imax) - minval(imax)))
        #:endif
    #:elif RANK == 2
        #:if RANK2 == 0
        randi_${RANK}$_${RANK2}$ = floor(randu(dim1, dim2)*real(imax)) + 1
        #:elif RANK2 == 1
        randi_${RANK}$_${RANK2}$ = minval(imax) + nint(randu(dim1, dim2)*real(maxval(imax) - minval(imax)))
        #:endif 
    #:elif RANK == 3
        #:if RANK2 == 0
        randi_${RANK}$_${RANK2}$ = floor(randu(dim1, dim2, dim3)*real(imax)) + 1
        #:elif RANK2 == 1
        randi_${RANK}$_${RANK2}$ = minval(imax) + nint(randu(dim1, dim2, dim3)*real(maxval(imax) - minval(imax)))
        #:endif
    #:endif
    return
    end procedure
    #:endfor
    #:endfor
end submodule
